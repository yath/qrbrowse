#!/usr/bin/perl
use 5.12.0;
use utf8;
use strict;
use warnings;
use open qw<:encoding(UTF-8) :std>;
use FindBin;
use lib "$FindBin::RealBin/lib";
use Regexp::Common qw(URI);
use File::Temp qw(tempdir);
use File::Spec::Functions qw(catfile);

my @patterns = (
    $RE{URI}{HTTP}, # from Regexp::Common
    qr/\d{4,}/,
);

my %toescape = qw(
    < &lt;
    > &gt;
    & &amp;
);

my @viewer = qw(w3m);

my $qrenc;

sub orpatterns {
    return $_[0] if @_ == 1;
    return "(?:".join(")|(?:", @_).")";
}

my $toescape = orpatterns(map { quotemeta } keys %toescape);
my $pattern = orpatterns(@patterns, $toescape);
my %qrfiles;
my $qrfile = 0;
my $tempdir = tempdir(CLEANUP => 1);

sub qrenc {
    my ($text) = @_;
    my $outfn = sprintf("qr_%d.txt", $qrfile++);
    open(my $outfh, ">", catfile($tempdir, $outfn))
        or die "Unable to open $outfn: $!\n";
    my $qr = $qrenc->encode($text);
    $qr =~ y/ */â–ˆ /;
    print $outfh $qr;
    close $outfh
        or die "Unable to close $outfn: $!\n";
    return $outfn;
}

sub qrfile {
    my ($text) = @_;
    if (!defined($qrfiles{$text})) {
        $qrfiles{$text} = qrenc($text);
    }
    my $fn = $qrfiles{$text};
    my $qtext = $text;
    $qtext =~ s/$toescape/$toescape{$&}/og;
    return qq(<a href="$fn">$qtext</a>);
}

my $textf = catfile($tempdir, "index.html");
open (my $textfh, ">", $textf)
    or die "Unable to open index file: $!\n";

for my $enc (qw(TextQRCode QREncode)) {
    undef $qrenc;
    eval {
        my $encfull = "QRBrowse::Encoders::$enc";
        eval "require $encfull" or die $@;
        no strict "refs";
        $qrenc = $encfull->new();
    };
    last unless $@;
}
die "No suitable QR encoder found!\n" unless $qrenc;

while (<>) {
    s/$pattern/
        exists($toescape{$&}) ?
        $toescape{$&} :
        qrfile($&)
    /oge;
    print $textfh $_."<br>";
}
close $textfh
    or die "Unable to close index file: $!";

system(@viewer, $textf);
